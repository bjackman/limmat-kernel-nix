# Derivation to build a kernel bzImage for a given kconfig.
# This can be used for VM testing, while being a much more lightweight build
# than a full NixOS kernel with every driver etc.
{
  pkgs,
  stdenv,

  # Required args:
  kernelSrc,
  # .config file, e.g. as generated by kconfig.nix
  kconfig,
}:
stdenv.mkDerivation {
  name = "bzImage";
  src = kernelSrc;
  # These probably aren't all needed, this was copied from the nixpkgs
  # manual-config.nix.
  nativeBuildInputs = with pkgs; [
    bison
    flex
    perl
    bc
    openssl
    rsync
    gmp
    libmpc
    mpfr
    elfutils
    zstd
    python3Minimal
    kmod
    hexdump
  ];
  enableParallelBuilding = true;
  # I thought you could point Kbuild to another .config file but apparently not.
  configurePhase = ''
    cp ${kconfig} .config
  '';
  buildFlags = [
    "bzImage"
  ];
  installPhase = ''
    cp arch/x86/boot/bzImage $out
  '';
}
